// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.5.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50"
        classpath "com.google.gms:google-services:4.2.0"
    }
}
plugins {
    id 'com.gradle.build-scan' version '2.1'
    id 'de.fayard.buildSrcVersions' version '0.6.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://mapbox.bintray.com/mapbox' }
        jcenter()
    }
}

tasks.register("install") {
    group = "install"
    dependsOn(":app:installDebug")
    description = "Install app via :app:installDebug"
}

tasks.register("hello") {
    group = "custom"
    description = "Hello World task - useful to solve build problems"
}

/**
 * Next time you need to update Gradle itself, run
 *   ./gradlew wrapper
 * It uses the "version.gradleLatestVersion" from "gradle.properties"
 ***/
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = findProperty("version.gradleLatestVersion")
}

/**
 * The Gradle Build Scan, one of those simple super-useful things the Android team never bothered to document properly
 * See https://guides.gradle.org/creating-build-scans/
 * See https://guides.gradle.org/performance/
 */
buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    //publishAlways()
}
